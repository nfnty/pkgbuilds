pkgname=('hostapd')
srcname='hostapd'
pkgver='2.5'
tag='hostap_2_5'
pkgrel='2'
pkgdesc='IEEE 802.11 AP, IEEE 802.1X/WPA/WPA2/EAP/RADIUS Authenticator'
arch=('i686' 'x86_64')
url='http://w1.fi/hostapd/'
license=('custom:BSD')

depends=('openssl' 'libnl')
makedepends=('git')
provides=('hostapd')
conflicts=('hostapd')

source=(
    "${srcname}::git+git://w1.fi/hostap.git#tag=${tag}"
    'config'
    'hostapd.service'
    '40mhz.patch'
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/40mhz.patch"

    cd hostapd
    cp "${srcdir}/config" .config
    sed --in-place 's#/etc/hostapd#/etc/hostapd/hostapd#' hostapd.conf
}

build() {
    cd "${srcdir}/${srcname}/hostapd"

    export CFLAGS="${CFLAGS} $( pkg-config --cflags libnl-3.0 )"
    make
}

package() {
    cd "${srcdir}/${srcname}"

    # Systemd unit
    install -D --mode=644 "${srcdir}/hostapd.service" "${pkgdir}/usr/lib/systemd/system/hostapd.service"

    # License
    install -D --mode=644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    cd hostapd

    # Binaries
    install --directory "${pkgdir}/usr/bin"
    install --target-directory="${pkgdir}/usr/bin" hostapd hostapd_cli

    # Configuration
    install --directory "${pkgdir}/etc/hostapd"
    install --directory "${pkgdir}/usr/share/doc/hostapd"
    install --mode=644 --target-directory="${pkgdir}/usr/share/doc/hostapd" \
        hostapd.{accept,conf,deny,eap_user,eap_user_sqlite,radius_clients,sim_db,vlan,wpa_psk} \
        wired.conf hlr_auc_gw.milenage_db

    # Man pages
    install -D --mode=644 hostapd.8 "${pkgdir}/usr/share/man/man8/hostapd.8"
    install -D --mode=644 hostapd_cli.1 "${pkgdir}/usr/share/man/man1/hostapd_cli.1"
}
