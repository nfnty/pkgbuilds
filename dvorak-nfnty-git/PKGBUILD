# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('dvorak-nfnty-git')
_srcname='dvorak-nfnty'
pkgver='r1'
pkgrel='1'
pkgdesc='Customized dvorak layout for console and Xorg'
arch=('any')
url='https://github.com/nfnty/dvorak-nfnty'
license=('MIT')

makedepends=('git' 'gzip')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    for path_file in kbd/*.map; do
        gzip --stdout "${path_file}" > "${path_file}.gz"
    done
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=644 --target-directory="${pkgdir}/usr/share/kbd/keymaps" kbd/*.map.gz

    cd xkb
    for directory in *; do
        install -D --mode=644 --target-directory="${pkgdir}/usr/share/X11/xkb/${directory}" "${directory}/"*
    done
}
