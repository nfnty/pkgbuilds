# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('lego')
_srcname='lego'
pkgdesc='Let'"'"'s Encrypt client and ACME library written in Go'
pkgver='3.0.2'
_commit_lego='fd11248e65c1d04a7a9d3902438d244ad9eef598'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/go-acme/${_srcname}"
license=('MIT')

makedepends=('git' 'go')

source=("${_srcname}::git+${url}.git#commit=${_commit_lego}")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    rm --recursive "${GOPATH}" &> /dev/null || true
    mkdir --parents "${GOPATH}/src/${_url_go}"
    rmdir "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    go get -v -d ./...
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go install -v ./...
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install -D --mode='755' "${GOPATH}/bin/lego" "${pkgdir}/usr/bin/lego"
}
