# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('influxdb')
_srcname='influxdb'
pkgdesc='Scalable datastore for metrics, events, and real-time analytics'
pkgver='1.1.1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/influxdata/${_srcname}"
license=('MIT')

makedepends=('git' 'go')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git#tag=v${pkgver}")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"

    go get github.com/sparrc/gdm
    "${GOPATH}/bin/gdm" restore
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go install -v -ldflags="\
-X main.version=${pkgver} \
-X main.branch=$( git rev-parse --abbrev-ref HEAD ) \
-X main.commit=$( git rev-parse HEAD ) \
-X main.buildtime=$( date --utc --iso-8601=seconds )" \
        ./...
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_srcname}",usr,"var/lib/${_srcname}"}
    install -D --target-directory="${pkgdir}/usr/bin" "${GOPATH}/bin/influx"*

    cd "${GOPATH}/src/${_url_go}"
    cp etc/config.sample.toml "${pkgdir}/etc/${_srcname}/config.toml"

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
