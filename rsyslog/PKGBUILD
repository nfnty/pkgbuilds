pkgname=('rsyslog')
srcname='rsyslog'
pkgdesc='a Rocket-fast SYStem for LOG processing'
pkgver='8.16.0'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/rsyslog/${srcname}"
license=('GPL3')
depends=(
    'zlib'
    'json-c'
    'libutil-linux'
    'libgcrypt'
    'liblogging'
    'libestr'
)
makedepends=(
    'git'
    'gcc'
    'make'
    'libtool'
    'python-docutils'
    'bison'
    'flex'
)
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${srcname}::git+https://github.com/rsyslog/${srcname}.git#tag=v${pkgver}"
    'tmpfiles.conf'
)
sha512sums=(
    'SKIP'
    'SKIP'
)

install='install.sh'

build() {
    cd "${srcdir}/${srcname}"

    autoreconf --install --force --warnings='all'
    ./configure --prefix='/usr' --sbindir='/usr/bin'

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install

    install --directory --owner='534' --group='534' --mode='750' "${pkgdir}/var/log/rsyslog"

    install --directory --mode='755' "${pkgdir}/usr/lib/tmpfiles.d"
    install --mode='644' "${srcdir}/tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/rsyslog.conf"
}
