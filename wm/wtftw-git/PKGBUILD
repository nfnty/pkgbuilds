pkgname=('wtftw-git')
srcname='wtftw'
pkgver='r1'
pkgrel='1'
pkgdesc='Window Tiling For The Win. A tiling window manager written in Rust'
arch=('i686' 'x86_64')
url='https://github.com/Kintaro/wtftw'
license=('custom:BSD')

depends=('rust' 'libxinerama')
makedepends=('git' 'cargo')
provides=('wtftw')
conflicts=('wtftw')

source=("${srcname}::git+https://github.com/Kintaro/wtftw.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${srcname}"

    export CARGO_HOME="${srcdir}/cargo"
    cargo build --release
}

package() {
    cd "${srcdir}/${srcname}"

    install -D --mode=755 target/release/wtftw "${pkgdir}/usr/bin/wtftw"
    install -D --mode=644 config/config.rs "${pkgdir}/usr/share/wtftw/config.rs"
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licences/wtftw/LICENSE"
}
