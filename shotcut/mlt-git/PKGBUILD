pkgname=('mlt-git')
_srcname='mlt'
pkgdesc='Multimedia Framework'
pkgver=6.26.1.r148.g941c6ca7
pkgrel=1
arch=('i686' 'x86_64')
url="https://github.com/mltframework/${_srcname}"
license=('GPL2')

optdepends=(
    'sdl_image: for SDL plugin'
    'libsamplerate: for libavresample plugin'
    'sox: for SOX (Audio Swiss Army Knife) plugin'
    'ffmpeg: for ffmpeg plugin'
    'vid.stab: for video stabilize plugin'
    'gtk2: Gtk plugin'
    'qt5-svg: for Qt5 plugins'
    'jack: for JACK sound output plugin'
    'libexif: for auto rotate plugin'
    'frei0r-plugins: for additional effects'
    'movit: opengl plugin'
)
makedepends=(
    'cmake'
    'ladspa'
    'frei0r-plugins'
    'libdv'
    'sdl_image'
    'libsamplerate'
    'sox'
    'ffmpeg'
    'vid.stab'
    'qt5-svg'
    'jack'
    'libexif'
    'python2'
    'swig'
    'git'
    'movit'
)
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cmake -S "${srcdir}/${_srcname}" -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
}
